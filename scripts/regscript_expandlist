# Awk script to read lists of the form:
#   var=value1:value2:...:valuek
# and output them as export statements that can be slurped into a shell
# script under cygwin.
#
# Each export statement will set var to a semicolon-separated list
# (for use with the windows JDK) with each of value1, ..., valuek
# expanded using cygpath.

# Variable assignments:
/^[_a-zA-Z][_a-zA-Z0-9]*=/ {
    # Determine where the '=' lies.
    equals = index($0, "=")
    # Extract the variable name and value.
    name = substr($0, 1, equals - 1)
    value = substr($0, equals + 1)
    # Split the value into its colon-separated components.
    total = split(value, components, ":")
    # Expand each component using cygpath.
    for (i in components)
        components[i] = "`cygpath -aw \"" components[i] "\"`"
    # Print the final export statement to feed back into the parent script.
    printf "export %s=\"", name
    if (i >= 1)
        printf "%s", components[1]
    for (i = 2; i <= total; i++)
        printf ";%s", components[i]
    print "\";"
}
