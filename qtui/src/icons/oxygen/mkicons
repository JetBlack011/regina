#!/usr/bin/perl -w
use strict;

# Oxygen icon sources:
# svn://anonsvn.kde.org/home/kde/trunk/kdesupport/oxygen-icons/scalable
# svn://anonsvn.kde.org/home/kde/tags/kdesupport-for-4.5/oxygen-icons/scalable

my @actionSizes = ( 16, 22, 32 );
my @dialogSizes = ( 16, 22, 32, 48, 64 );
my @actionIcons = qw/
    application-exit
    application-xml
    arrow-down
    arrow-down-double
    arrow-left
    arrow-right
    arrow-up
    arrow-up-double
    bookmarks
    configure
    dialog-cancel
    dialog-ok
    document-new
    document-open
    document-open-recent
    document-save
    document-save-as
    document-sign
    edit-copy
    edit-cut
    edit-delete
    edit-paste
    edit-rename
    edit-select-all
    edit-table-delete-row
    edit-table-insert-row-below
    go-bottom
    go-top
    help-about
    help-contents
    help-contextual
    list-add
    list-remove
    mail-attachment
    process-stop
    run-build-file
    text-x-c++src
    utilities-terminal
    view-refresh
    window-close
    /;
my @dialogIcons = qw/
    clock
    dialog-warning
    dialog-information
    help-hint
    system-run
    /;

sub usage {
    my $err = shift;
    $err and print STDERR "ERROR: $err\n\n";
    print STDERR "Usage: mkicons <path_to_scalable_checkout>\n";
    exit 1;
}

sub render {
    my $icon = shift;
    my $size = shift;
    my $filename = shift;
    my $baseDir = shift;

    my $src = "$baseDir/small/${size}x${size}/$icon.svgz";
    -e $src or $src = "$baseDir/$icon.svgz";
    -e $src or $src = "$baseDir/small/48x48/$icon.svgz";
    if (not -e $src) {
        print STDERR "ERROR: Could not find source for $icon at size $size.\n";
        return;
    }

    `rsvg -w$size -h$size "$src" "$filename"`;
}

sub process {
    my $icon = shift;
    my $sizes = shift;
    my $svn = shift;

    my @src = glob("$svn/*/$icon.svgz");
    my $baseDir = undef;
    if ($#src > 0) {
        print STDERR "ERROR: Ambiguous sources for $icon:\n";
        foreach (@src) {
            print STDERR "$_\n";
        }
        return;
    }
    if ($#src == 0) {
        $baseDir = $src[0];
        $baseDir =~ s/\/[^\/]+.svgz//;
    } else {
        @src = glob("$svn/*/small/48x48/$icon.svgz");
        if ($#src > 0) {
            print STDERR "ERROR: Ambiguous sources for $icon:\n";
            foreach (@src) {
                print STDERR "$_\n";
            }
            return;
        }
        if ($#src == 0) {
            $baseDir = $src[0];
            $baseDir =~ s/\/small\/48x48\/[^\/]+.svgz//;
            print "Resorting to 48x48 icon for $icon.\n";
        }
    }

    if (not $baseDir) {
        print STDERR "ERROR: No sources found for $icon\n";
        return;
    }

    foreach (@$sizes) {
        render($icon, $_, "$icon-$_.png", $baseDir);
        render($icon, 2 * $_, "$icon-$_\@2x.png", $baseDir);
    }
}

$#ARGV == 0 or usage;

my $svn = $ARGV[0];
-d "$svn" or usage "Argument \"$svn\" is not a directory.";
-e "$svn/actions/window-close.svgz" or
    usage "Argument \"$svn\" does not look like an oxygen/scalable checkout.";

foreach my $i (@actionIcons) {
    process($i, \@actionSizes, $svn);
}

foreach my $i (@dialogIcons) {
    process($i, \@dialogSizes, $svn);
}

